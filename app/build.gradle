apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "$project.rootDir/secretconfig.gradle"

android {
    compileSdkVersion COMPLIE_SDK_VERSION
    defaultConfig {
        applicationId "ru.popov.bodya.howmoney"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        config {
            keyAlias signConfigs.keyAlias
            keyPassword signConfigs.keyPassword
            storeFile file(signConfigs.storeFile)
            storePassword signConfigs.storePassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "FIXER_API_KEY", "\""+apiKeys.fixerIo+"\""
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // modules
    implementation project(':core')
    // support
    implementation libraries.supportAppCompat
    implementation libraries.supportDesign
    implementation libraries.supportConstraintLayout
    implementation libraries.supportPreference
    // kotlin
    implementation libraries.kotlin
    // dagger
    implementation libraries.dagger
    implementation libraries.daggerAndroid
    kapt libraries.daggerAndroidProcessor
    kapt libraries.daggerCompiler
    // rxjava
    implementation libraries.rxKotlin
    implementation libraries.rxAndroid
    // moxy
    implementation libraries.moxy
    implementation libraries.moxyAndroid
    implementation libraries.moxyAppCompat
    kapt libraries.moxyCompiler
    // navigation
    implementation libraries.cicerone
    // network
    implementation libraries.retrofit
    implementation libraries.retrofitGsonConverter
    implementation libraries.retrofitRxJavaAdapter
    // custom views
    implementation libraries.decoview
    // test
    testImplementation libraries.junit
    testImplementation libraries.hamcrest
    testImplementation libraries.mockito
}
